#! /usr/bin/perl

use strict;
use warnings FATAL => 'all';
use 5.10.1;
use autodie qw{:all};
use Desktop::Notify;
use Locale::gettext;
use Net::DBus::Reactor;
use POSIX;
use Path::Class;
use String::Errf qw{errf};

setlocale(LC_MESSAGES, "");
textdomain("tails");


=head1 Functions

=head2 current_lang

Returns the two-letters language code of the current session.

=cut
sub current_lang {
    my ($code) = ($ENV{LANG} =~ m/([a-z]{2}).*/);

    return $code;
}

=head2 doc_url

Returns the best local URL to a given piece of doc.

=cut
sub doc_url {
    my $website_root = shift;
    my $doc_resource = shift;

    my @try_files = (
        file($website_root, $doc_resource . "." . current_lang() . ".html" ),
        file($website_root, $doc_resource . ".en.html" ),
    );

    foreach my $file (@try_files) {
        return "file://$file" if -e $file;
    }

    return;
}


=head1 Callbacks

=cut

sub action_cb {
    my $reactor = shift;

    my $website_root = '/usr/share/doc/tails/website';

    my $doc_insecure_disabled_resource = 'doc/first_steps/persistence/recover_insecure';
    my $doc_insecure_disabled_url = doc_url($website_root, $doc_insecure_disabled_resource)
        or die "Could not find best URL for '$doc_insecure_disabled_resource' at '$website_root'";

    system('/usr/local/bin/tor-browser', $doc_insecure_disabled_url);

    $reactor->shutdown;
}


=head1 Main

=cut

my @files_disabled_for_wrong_access_rights = (
    glob('/live/persistence/*_unlocked/persistence.conf.insecure_disabled'),
    glob('/live/persistence/*_unlocked/live-additional-software.conf.insecure_disabled'),
);

@files_disabled_for_wrong_access_rights || exit 0;

my $reactor = Net::DBus::Reactor->main;

my $notify = Desktop::Notify->new();
$notify->action_callback(sub { action_cb($reactor, @_) });
$notify->close_callback(sub { $reactor->shutdown; });

my $summary = gettext(q{Some persistence settings were temporarily disabled});
my $body    = errf(
    gettext("%{disabled_conf_files}s\n"),
    {
        disabled_conf_files => join(
            ', ',
            @files_disabled_for_wrong_access_rights
        ),
    }
);

$notify->create(
    summary => $summary,
    body    => $body,
    actions => { 'moreinfo'  => gettext('Learn how to enable them again'), },
    hints   => { 'transient' => 1, },
    timeout => 0)->show();

$reactor->run;
